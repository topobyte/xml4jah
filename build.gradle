task wrapper(type: Wrapper) {
    gradleVersion = 2.3
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':xml4jah-core'),
    project(':xml4jah-dom')
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        mavenCentral()
    }
}

configure(libraries) {

    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    def runForArtifactPublication = project.hasProperty("topobyte")

    if (runForArtifactPublication) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    artifacts {
        archives sourcesJar
    }

    def global = new File(rootProject.projectDir, 'info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def projName = pomInfo.name
    def projDesc = pomInfo.description
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        projName = pomInfoL.name
        projDesc = pomInfoL.description
    }

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom.project {
                        name projName
                        description projDesc
                        url pomInfo.url
                        licenses {
                            license {
                                name pomInfo.licenseName
                                url pomInfo.licenseUrl
                                distribution pomInfo.licenseDistribution
                            }
                        }
                    }
                    pom.whenConfigured {
                        p -> p.dependencies = p.dependencies.findAll {
                            dep -> dep.scope != "test"
                        }
                    }
                }
            }
        }
    }
}
